module uart_receive(
    input clk,//50M 
    input rx,//uart rx
    output reg data_en,
    output reg [7:0] data_out
    );
    
	initial data_en <= 1'b0;
	initial data_out <= 8'd0;
		
	parameter DIV_NUM = 1736;//clk/baudrate/3  
	parameter WIDTH = 11;//DIV_NUM  width

	//input buffer
	reg  [1:0]rx_buf;
	reg [WIDTH-1:0] cnt;
	reg [1:0]state;
	//3x sample buffer
	reg [1:0]samples;
	reg [2:0]rev_cnt;
	reg [1:0]cnt3;
	reg [7:0]data_sfg;

	initial cnt <= {WIDTH{1'b0}};
	initial state <= 2'b00;
	initial rx_buf <= 2'b0;
	initial rev_cnt <= 3'b0; 
	initial data_sfg <= 8'd0;

	always @(posedge clk)
	begin
		rx_buf[1:0] <= {rx_buf[0],rx};
		case(state)
		2'b00:begin
			if(rx_buf==2'b10)begin
				cnt <= (DIV_NUM-1)/2;
				cnt3 <= 2'b00;
				state <= 2'b01;
			end else begin
				cnt <= {WIDTH{1'b0}};
			end
			data_en <= 1'b0; 
		end
		2'b01:begin
			if(cnt == DIV_NUM-1)begin
				cnt <= {WIDTH{1'b0}};
				samples <= {samples[0],rx_buf[1]};
				if(cnt3==2'b10)begin
					if({samples[1:0],rx_buf[1]}==3'b000)begin
						state <= 2'b10;
						cnt3 <= 2'b00;
						rev_cnt <= 4'd0;
					end else begin
						state <= 2'b00;
					end
				end else begin
					cnt3 <= cnt3 + 2'b01;
				end
			end else begin
				cnt <= cnt + {{(WIDTH-1){1'b0}},1'b1};
			end
		end
		2'b10:begin
			if(cnt == DIV_NUM-1)begin
				cnt <= {WIDTH{1'b0}};
				samples <= {samples[0],rx_buf[1]};
				if(cnt3==2'b10)begin
					cnt3 <= 2'b00;
					data_sfg[6:0] <= data_sfg[7:1];
					case ({samples[1:0],rx_buf[1]})
						3'b011   : data_sfg[7] <= 1'b1;
						3'b101   : data_sfg[7] <= 1'b1;
						3'b110   : data_sfg[7] <= 1'b1;
						3'b111   : data_sfg[7] <= 1'b1;					
						default : data_sfg[7] <= 1'b0;
					endcase
					if(rev_cnt == 3'd7)begin
						rev_cnt <= 3'd0;
						state <= 2'b11;
					end else begin
						rev_cnt <= rev_cnt + 3'd1;
					end
				end else begin
					cnt3 <= cnt3 + 2'b01;
				end
			end else begin
				cnt <= cnt + {{(WIDTH-1){1'b0}},1'b1};
			end
		end
		2'b11:begin
			state <= 2'b00;
			data_en <= 1'b1;
			data_out <= data_sfg[7:0];
		end
		endcase        
	end
endmodule 